apply plugin: 'maven'
apply plugin: 'signing'

/*
ext {
    pomVersion = lib_version
    pomGroupId = '*'
    // pomArtifactId, pomArtifactName, pomArtifactDescription

    pomSiteUrl = '*'
    pomGitUrl = '*.git'

    pomLicenses = {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    pomDevelopers = {
        developer {
            id '*'
            name '*'
            email '*'
            // organization, organizationUrl
        }
    }
}
apply from: 'publishing.gradle'
$ gradle uploadArchives
*/

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

def localProperties = new Properties()
def localFile = rootProject.file("local.properties")
if (localFile.exists()) {
    localProperties.load(localFile.newDataInputStream())
}

def props = { propName, defaultValue = "" ->
    def local = localProperties.containsKey(propName)

    if (local) return localProperties.getProperty(propName)
    else return System.getenv(propName) ?: defaultValue
}

gradle.taskGraph.whenReady { taskGraph ->
    ext["signing.keyId"] = props("signing.keyId")
    ext["signing.password"] = props("signing.password")
    ext["signing.secretKeyRingFile"] = props("signing.secretKeyRingFile")
}

signing {
    required { !props("nexus.repository").isEmpty() }
    sign configurations.archives
}

uploadArchives {
    // Authentication
    def username = props("nexus.username")
    def password = props("nexus.password")
    def repositoryUrl = props("nexus.repository")

    println "Project: $project.path"
    println "Username: $username"
    println "Repo URL: $repositoryUrl"

    repositories.mavenDeployer {
        if (repositoryUrl.isEmpty()) {
            repository(url: repositories.mavenLocal().url)
        } else {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: repositoryUrl) {
                authentication(userName: username, password: password)
            }
        }

        pom {
            groupId = pomGroupId
            artifactId = pomArtifactId
            version = pomVersion
        }

        pom.project {
            packaging 'aar'

            name pomArtifactName
            description pomArtifactDescription

            url pomSiteUrl

            licenses(pomLicenses)
            developers(pomDevelopers)

            scm {
                connection = pomGitUrl
                developerConnection = pomGitUrl
                url = pomSiteUrl
            }
        }
    }
}
